import java.text.SimpleDateFormat

static def time() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("yyyyMMddHHmmss")
    return df.format(today)
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "com.classic.assistant.car"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        resConfigs "zh-rCN"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
    // signingConfigs {
    //     Classic {
    //         storeFile file(uri("../tools/xxx.jks"))
    //         storePassword STORE_PASSWORD
    //         keyAlias KEY_ALIAS
    //         keyPassword KEY_PASSWORD
    //     }
    // }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
            // signingConfig signingConfigs.Classic
            project.gradle.startParameter.excludedTaskNames.add('lint')  // 屏蔽lint耗时检查
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // signingConfig signingConfigs.Classic
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "CarAssistant_" + variant.versionName + "_" + variant.versionCode + "_" + time() + ".apk"
    }
}

dependencies {
    // Kotlin and Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    // AndroidX
    implementation "com.google.android.material:material:$google_material_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.vectordrawable:vectordrawable:$vector_drawable_version"
    implementation "androidx.recyclerview:recyclerview:$recycler_view_version"
    implementation "androidx.cardview:cardview:$card_view_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    // ktx
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.sqlite:sqlite-ktx:$sqlite_version"
    implementation "androidx.activity:activity:$activity_version"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.collection:collection-ktx:$collection_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Other
    implementation "pub.devrel:easypermissions:3.0.0"
    implementation "com.classic.file.choose:easy-file-choose:0.5"
    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "com.contrarywind:Android-PickerView:4.1.9"
    implementation "com.amap.api:location:5.2.0"
    implementation "com.afollestad.material-dialogs:core:$material_dialog_version"
    implementation "com.afollestad.material-dialogs:lifecycle:$material_dialog_version"
    // Bugly
    implementation 'com.tencent.bugly:crashreport:3.2.422'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.5'

    // Flurry
    implementation "com.flurry.android:analytics:12.9.0"// @aar
    // Pgyer
    implementation 'com.pgyer:analytics:4.1.6'

    implementation "com.simplecityapps:recyclerview-fastscroll:2.0.1"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}